Forig = read("./data/USCensus.csv", data_type="frame", format="csv", header=TRUE);
F = Forig;

# data preparation
jspec= "{ ids:true, recode:[2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,"
+"21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,"
+"41,42,43,44,45,47,48,49,50,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,68,69], bin:["
+"{id:46, method:equi-width, numbins:10},"
+"{id:51, method:equi-width, numbins:10},"
+"{id:67, method:equi-width, numbins:10}]}"

[X,M] = transformencode(target=F, spec=jspec);
X = X[,2:ncol(X)] #drop id

# one hot encoding
m = nrow(X)
n = ncol(X)
fdom = colMaxs(X);
foffb = t(cumsum(t(fdom))) - fdom;
foffe = t(cumsum(t(fdom)))
rix = matrix(seq(1,m)%*%matrix(1,1,n), m*n, 1)
cix = matrix(X + foffb, m*n, 1);
X2 = table(rix, cix); #one-hot encoded

# create lables via clustering
# NOTE: the paper experiments did NOT specify the seed --> randomized init
# (the original error vector is stored as data/USCensus_o_e.csv; for 
# repeating the experiments with specified or random seed, decomment line 37)
[C,y] = kmeans(X=X2, k=4, seed=7)

# learn model
B = multiLogReg(X=X2, Y=y, icpt=1, reg=0.0001, verbose=TRUE);
[M,yhat,acc] = multiLogRegPredict(X=X2, B=B, Y=y, verbose=TRUE);
e = (y!=yhat);

write(X, "data/USCensus_X.csv", format="csv")
#write(e, "data/USCensus_o_e.csv", format="csv")
